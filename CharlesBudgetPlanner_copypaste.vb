10 REM DEF VARIABLES 
20 SZ=100:I=-19 
30 R$=CHR$(13):TA=0 
40 DIM A$(SZ),AE(SZ) 
50 DEFFNRN(X)=INT(X*100+.5)/100 
200 REM MAINROUTINE 
210 GOSUB6000 
220 Z$="":GETZ$:IFZ$=""THENGOTO220 
230 IFZ$=CHR$(133)THENI=I+20:GOSUB1000 
235 IFZ$=CHR$(134)THENGOSUB3000 
240 IFZ$=CHR$(135)THENGOSUB5000 
245 IFZ$=CHR$(136)THENGOSUB7000 
250 IFZ$=CHR$(137)THENGOSUB2000 
255 IFZ$=CHR$(138)THENGOSUB4000 
260 IFZ$=CHR$(139)THENGOSUB6000 
265 IFZ$=CHR$(140)THENGOSUB8000 
270 IFZ$=CHR$(17)THENI=I-1:GOSUB1000 
275 IFZ$=CHR$(145)THENI=I+1:GOSUB1000 
299 GOTO220 
300 REM ACCUM TOTALS 
310 TA=0 
320 FOR J=1TOMX 
330 TA=TA+AE(J) 
340 NEXTJ 
399 RETURN 
400 REM LOAD FILES 
410 INPUT"FILE NAME";F$ 
420 IFF$="*END"THENGOSUB6000:RETURN
450 OPEN1,8,2,F$+",SEQ,READ" 
455 PRINTCHR$(18)CHR$(30)"FOUND"CHR$(146)CHR$(144);F$ 
460 INPUT#1,MX 
470 FORJ=1TOMX 
480 INPUT#1,Y,A$(J),AE(J) 
490 NEXTJ 
495 CLOSE1
499 RETURN 
500 REM SORT BY NAME 
505 IFMX=1THENGOTO599 
510 PRINTCHR$(17)CHR$(17)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
511 PRINTCHR$(18)"SORTING"CHR$(146) 
520 FORJ=1TOMX-1 
530 FORK=J+1TOMX 
540 IFA$(K)>A$(J)THENGOTO590 
550 SM$=A$(K):SM=AE(K) 
560 A$(K)=A$(J) :AE(K)=AE(J) 
570 A$(J)=SM$:AE(J)=SM 
590 NEXTK 
595 NEXTJ 
599 RETURN 
1000 REM DISPLAY 
1010 IF(I<1)OR(I>MX)THENI=1 
1020 PRINTCHR$(147)" #"TAB(5)CHR$(159)"EXPENSES"CHR$(144)TAB(16)CHR$(156)"AMT";
1021 PRINTCHR$(144)
1030 FORJ=ITOI+19
1040 IFJ>MXTHENPRINT" ":GOTO1080
1050 PR$=STR$(AE(J)+.001):PR$=MID$(PR$,2,(LEN(PR$)-2))
1060 IFAE(J)=0THENPR$="0.00"
1065 J$=MID$(STR$(J),2)
1070 PRINTTAB(3-LEN(J$))J$;TAB(4)A$(J)TAB(21-LEN(PR$))PR$
1080 NEXTJ
1090 TA$=STR$(TA+.001)
1100 TA$=LEFT$(TA$,LEN(TA$)-1)
1110 IFTA=0THENTA$="0.00"
1120 PRINTCHR$(159)"TOTAL "CHR$(144)TA$
1999 RETURN
2000 REM ADD NEW
2010 R=MX+1:N$="":E1$=""
2020 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)"ADD NEW EXPENSES"
2030 PRINTCHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
2031 PRINTCHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
2032 PRINTCHR$(17)"ITEM #";R;CHR$(17)
2040 INPUT"ITEM NAME ";N$
2050 IFN$="*END"THENGOTO2999
2055 IFLEN(N$)>10THENN$=LEFT$(N$,10)
2060 A$(R)=N$
2070 PRINTCHR$(17);
2071 INPUT"ITEM AMT  ";E1$
2080 IFE1$="*END"THENGOTO2999
2085 IFVAL(E1$)=0THENAE(R)=0:GOTO2100
2090 AE(R)=FNRN(VAL(E1$))
2095 IFAE(R)>9999.99THENAE(R)=9999.99
2100 MX=MX+1
2110 GOTO2010
2220 MX=MX+1
2999 GOSUB500:GOSUB300:GOSUB6000:RETURN
3000 REM UPDATE
3010 PRINTCHR$(147)CHR$(31)" EXPENSE ";CHR$(18)"UPDATE"CHR$(146)CHR$(144);
3020 PRINTCHR$(13)
3021 INPUT"ITEM # ";P1$
3025 IFP1$="*END"THENGOTO3999
3026 IF(VAL(P1$)=0)OR(VAL(P1$)<1)THENPRINTCHR$(13)CHR$(13)CHR$(29)CHR$(29);
3027 IF(VAL(P1$)=0)OR(VAL(P1$)<1)THENPRINTCHR$(29)CHR$(29)CHR$(156)CHR$(18);
3028 IF(VAL(P1$)=0)OR(VAL(P1$)<1)THENPRINT"INPUT ERROR"CHR$(146)CHR$(144);"
3029 IF(VAL(P1$)=0)OR(VAL(P1$)<1)THENGOTO3020
3030 P=INT(VAL(P1$))
3032 N$="":E1$=""
3040 IFP>SZTHENPRINT"MAX EXCEEDED":P=SZ:MX=P
3050 IFP>MXTHENMX=P
3060 PR$=STR$(AE(P) +.001):PR$=MID$(PR$,2,(LEN(PR$)-2))
3065 IFAE(P)=0THENPR$="0.00"
3070 PRINTP;TAB(4)A$(P)TAB(21-LEN(PR$))PR$CHR$(17)
3080 INPUT"ITEM NAME";N$
3090 IFN$="*END"THENGOTO3999
3100 IFN$<>""THENA$(P)=N$
3105 IFLEN(A$(P))>10THENA$(P)=LEFT$(A$(P),10)
3110 INPUT"AMT ";E1$
3120 IFE1$="*END"THENGOTO3999
3125 IFE1$=""GOTO3010
3130 IF(VAL(E1$)=0)AND(E1$<>"0")THENPRINTCHR$(13)CHR$(13)CHR$(29)CHR$(29);
3131 IF(VAL(E1$)=0)AND(E1$<>"0")THENPRINTCHR$(29)CHR$(18)CHR$(156)CHR$(18);
3133 IF(VAL(E1$)=0)AND(E1$<>"0")THENPRINT"INPUT ERROR"CHR$(146)CHR$(144)
3134 IF(VAL(E1$)=0)AND(E1$<>"0")THENGOTO3110
3135 IFVAL(E1$)=0THENAE(P)=0:GOTO3800
3140 AE(P)=FNRN(VAL(E1$))
3150 IFAE(P)>9999.99THENAE(P)=9999.99
3800 GOTO3010
3999 GOSUB500:GOSUB300:GOSUB6000;RETURN
4000 REM SAVE FILE
4010 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)"SAVE EXPENSE LIST"CHR$(13)CHR$(13);
4020 INPUT"FILE NAME";F$
4030 IFF$="*END"THENGOSUB6000:RETURN
4050 OPEN1,8,2,F$+",SEQ,WRITE"
4060 PRINT#1,MX
4070 FORJ=1TOMX
4080 PRINT#1,J;R$;A$(J)R$;AE(J);R$
4090 NEXTJ
4100 CLOSE1 :REM 108
4999 GOSUB6000:RETURN
5000 REM DELETE
5005 DT=0:TM=0
5010 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
5011 PRINTCHR$(29)"DELETE"
5020 S1$=""
5021 PRINTCHR$(13)CHR$(13)
5030 INPUT"START AT";S1$
5040 IFS1$="*END"THENGOTO5900
5050 DS=INT(VAL(S1$))
5060 S1$=""
5070 IFDS=0THENPRINTCHR$(17)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
5071 IFDS=0THENPRINTCHR$(18)CHR$(156)"INPUT ERROR"CHR$(146);
5073 IFDS=0THENPRINTCHR$(144):GOTO5020
5080 S1$=""
5081 PRINTCHR$(13)CHR$(13);
5090 INPUT"END AT";S1$
5100 IFS1$="*END"THENGOTO5900
5110 IFS1$=""ORS1$="0"THENDE=0:GOTO5200
5120 DE=INT(VAL(S1$))
5125 IFDE>MXTHENDE=MX
5130 IFDE=>DSTHENGOTO5200
5135 PRINTCHR$(13)CHR$(13)CHR$(29)CHR$(29)CHR$(18)CHR$(156);
5136 PRINT"0 OR NUMBER GREATER"
5140 PRINTCHR$(13)CHR$(13)CHR$(29)CHR$(29);
5141 PRINT"THAN "CHR$(146)CHR$(28);DE;CHR$(18)CHR$(156);
5142 PRINT"REQUIRED"CHR$(144);
5150 GOTO5080
5200 IFDE=0THENDE=DS
5205 TM=DE-DS+1
5207 DT=DT+TM
5210 FORJ=DSTODE
5220 A$(J)="<9 B>":AE(J)=0 :REM "THIS LINE MIGHT NEED AN EDIT
5230 NEXTJ
5240 GOTO5010
5900 GOSUB500
5910 MX=MX-DT
5999 GOSUB300:GOSUB6000:RETURN
6000 REM OPTIONS MENU
6009 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
6010 PRINTCHR$(156)"OPTIONS: ",CHR$(144)"
6020 PRINTCHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(158);
6021 PRINT"========";CHR$(144)
6030 PRINTCHR$(17)CHR$(18)CHR$(156);"F1";CHR$(146)CHR$(144);"-DISPLAY EXPENSES"
6040 PRINTCHR$(17)CHR$(18)CHR$(156);"F2";CHR$(146)CHR$(144);"-ADD NEW EXPENSES"
6050 PRINTCHR$(17)CHR$(18)CHR$(156);"F3";CHR$(146)CHR$(144);
6051 PRINT"-UPDATE EXPENSE LIST"
6060 PRINTCHR$(17)CHR$(18)CHR$(156);"F4";CHR$(146)CHR$(144);"-SAVE EXPENSE LIST"
6070 PRINTCHR$(17)CHR$(18)CHR$(156);"F5";CHR$(146)CHR$(144);"-DELETE FROM LIST"
6080 PRINTCHR$(17)CHR$(18)CHR$(156);"F6";CHR$(146)CHR$(144);"-OPTIONS SCREEN"
6090 PRINTCHR$(17)CHR$(18)CHR$(156);"F7";CHR$(146)CHR$(144);"-LOAD/MERGE FILES"
6100 PRINTCHR$(17)CHR$(18)CHR$(156);"F8";CHR$(146)CHR$(144);"-END"
6999 RETURN 
7000 REM LOAD/MERGE
7010 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)"LOAD/MERGE"
7020 PRINTCHR$(17)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)"EXPENSE FILES"
7030 INPUT"LOAD OR MERGE (L/M)";AN$
7040 IFAN$="L"THENMX=0:GOSUB400:GOTO7999
7050 IFAN$="*END"THENGOSUB6000:RETURN
7060 IFAN$<>"M"GOTO7030
7070 PRINTCHR$(17)CHR$(29)CHR$(29)CHR$(29)CHR$(29)"MERGE"CHR$(17)
7080 INPUT"FILE NAME";F$
7090 IFF$="*END"THENGOSUB6000:RETURN
7120 OPEN1,8,2,F$+",SEQ,READ"
7130 INPUT#1,T1
7140 FORT2=1TOT1
7150 INPUT#1,Y,T3$,T4
7160 FORJ=1TOMX
7170 IFA$(J)=T3$THENAE(J)=INT(((AE(J)+T4)/2)*100)/100:T3$=""
7180 NEXTJ
7190 IFT3$<>""THENMX=MX+1:A$(MX)=T3$:AE(MX)=T4
7200 NEXT
7210 CLOSE1
7999 GOSUB500:GOSUB300:GOSUB6000:RETURN
8000 REM END OF JOB
8010 PRINTCHR$(147)CHR$(29)CHR$(29)CHR$(29)CHR$(29)"END OF PROGRAM";
8011 PRINTCHR$(13)CHR$(13)
8020 PRINT"WOULD YOU LIKE TO SAVE (Y/N)":INPUT AN$
8030 IFAN$="*END"THENGOSUB6000:RETURN
8040 IFAN$="N"THENGOTO8060
8050 GOSUB4000
8060 PRINTCHR$(147)"THANK YOU"
8070 PRINTCHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
8071 PRINTCHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29)CHR$(29);
8072 PRINTCHR$(29)"END"
8075 PRINT CHR$(154)
8080 END

